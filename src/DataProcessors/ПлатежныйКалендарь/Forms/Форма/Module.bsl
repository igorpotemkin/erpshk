
&НаСервере
Процедура Расш1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	//+abaturina@syspod.ru #77028
	ИмяКоманды = "__РеестрПлатежейПоЗаявителю";
	
	нКоманда = ЭтаФорма.Команды.Добавить(ИмяКоманды);
	нКоманда.Действие = "РеестрПлатежейПоЗаявителю";
	
	нКнопка = ЭтаФорма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭтаФорма.Элементы.ГруппаКомандыДерева);
	нКнопка.Вид         = ВидКнопкиФормы.КнопкаКоманднойПанели;
	нКнопка.Заголовок   = "Реестр платежей по заявителю";
	нКнопка.ИмяКоманды  = ИмяКоманды;
	нКнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	нКнопка.Картинка    = БиблиотекаКартинок.Отчет;
	//abaturina@syspod.ru #77028

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ИзменитьВидимостьЭлементовФормы")
Процедура Расш1_ИзменитьВидимостьЭлементовФормы()

	Элементы.ИзменитьРежим.Картинка = ЭтаФорма.Команды["Режим" + РежимОтображения].Картинка;

	ЗаявкиВидны = (РежимОтображения = "СписокЗаявок" Или РежимОтображения = "ЗаявкиКалендарь");
	КалендарьВиден = (РежимОтображения = "КалендарьПлатежи" Или РежимОтображения = "ЗаявкиКалендарь");

	Элементы.ГруппаЛеваяКолонка.Видимость  = ЗаявкиВидны;
	Элементы.ГруппаВКалендаре.Видимость    = Не ЗаявкиВидны;

	Элементы.ГруппаПраваяКолонка.Видимость         = КалендарьВиден;
	Элементы.СдвинутьВверх.Видимость               = КалендарьВиден;
	Элементы.СдвинутьВниз.Видимость                = КалендарьВиден;
	Элементы.ДеревоПлатежейСвернутьВсе.Видимость   = КалендарьВиден;
	Элементы.ДеревоПлатежейРазвернутьВсе.Видимость = КалендарьВиден;

	Элементы.ФильтрСчетовПодходящие.Видимость = ЗаявкиВидны И КалендарьВиден;

	Если КалендарьВиден Тогда
		Элементы.Переместить(Элементы.ГруппаКомандыДерева, Элементы.ГруппаПраваяКолонка, Элементы.ГруппаПанельПлатежей);
	Иначе
		Элементы.Переместить(Элементы.ГруппаКомандыДерева, Элементы.ГруппаКомандыЗаявки);
	КонецЕсли;

	Элементы.ГруппаФильтрСчетовИКасс.Видимость = (РежимОтображения = "ЗаявкиКалендарь");
	Элементы.ОтборБанковскийСчетКасса.Видимость = (РежимОтображения = "КалендарьПлатежи");

	Если РежимОтображения = "СписокЗаявок" Тогда
		Элементы.Переместить(Элементы.СформироватьСписанияДС, Элементы.ГруппаКоманднаяПанельЗаявки);
		Элементы.Переместить(Элементы.ГруппаПланирование, Элементы.ГруппаКоманднаяПанельЗаявки);
		Элементы.Переместить(Элементы.РеестрПлатежей, Элементы.ГруппаКоманднаяПанельЗаявки);
		#Вставка
		//+abaturina@syspod.ru #77028
		Если ЭтаФорма.Элементы.Найти("__РеестрПлатежейПоЗаявителю") <> Неопределено Тогда
			Элементы.Переместить(Элементы.__РеестрПлатежейПоЗаявителю, Элементы.ГруппаКоманднаяПанельЗаявки);
		КонецЕсли;
		//abaturina@syspod.ru #77028
		#КонецВставки
	ИначеЕсли РежимОтображения = "ЗаявкиКалендарь" Тогда
		Элементы.Переместить(Элементы.ГруппаПланирование, Элементы.ГруппаКомандыДерева);
		Элементы.Переместить(Элементы.СформироватьСписанияДС, Элементы.ГруппаКоманднаяПанельЗаявки);
		Элементы.Переместить(Элементы.РеестрПлатежей, Элементы.ГруппаКомандыДерева);
		#Вставка
		//+abaturina@syspod.ru #77028
		Если ЭтаФорма.Элементы.Найти("__РеестрПлатежейПоЗаявителю") <> Неопределено Тогда
			Элементы.Переместить(Элементы.__РеестрПлатежейПоЗаявителю, Элементы.ГруппаКомандыДерева);
		КонецЕсли;
		//abaturina@syspod.ru #77028
		#КонецВставки
	Иначе
		Элементы.Переместить(Элементы.ГруппаПланирование, Элементы.ГруппаКомандыДерева);
		Элементы.Переместить(Элементы.СформироватьСписанияДС, Элементы.ГруппаСформироватьПлатежи);
		Элементы.Переместить(Элементы.РеестрПлатежей, Элементы.ГруппаКомандыДерева);
		#Вставка
		//+abaturina@syspod.ru #77028
		Если ЭтаФорма.Элементы.Найти("__РеестрПлатежейПоЗаявителю") <> Неопределено Тогда
			Элементы.Переместить(Элементы.__РеестрПлатежейПоЗаявителю, Элементы.ГруппаКомандыДерева);
		КонецЕсли;
		//abaturina@syspod.ru #77028
		#КонецВставки
	КонецЕсли;

КонецПроцедуры

//+abaturina@syspod.ru #77028
&НаКлиенте
Процедура РеестрПлатежейПоЗаявителю(Команда)
	
	ПараметрыФормы = Новый Структура("Отбор, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии, ВидимостьКомандВариантовОтчетов");
	ПараметрыФормы.СформироватьПриОткрытии         = Истина;
	ПараметрыФормы.ВидимостьКомандВариантовОтчетов = Истина;
	ПараметрыФормы.КлючНазначенияИспользования     = "РеестрПлатежейПоЗаявителю";
	ПараметрыФормы.КлючВарианта                    = "РеестрПлатежейПоЗаявителю";
	
	ДатаПлатежа = ДенежныеСредстваКлиентСервер.ДатаПлатежа(Объект.ПланироватьСДаты, ДнейПланирования);
	
	Отбор = Новый Структура();
	Отбор.Вставить("Период", Новый СтандартныйПериод(Объект.ПланироватьСДаты, ДатаПлатежа));
	
	Если ЗначениеЗаполнено(ОтборОрганизацииПредставление) Тогда
		Организации = Новый Массив();
		
		Для Каждого ЭлементОтбора ИЗ Объект.ОтборОрганизации Цикл
			Если ЭлементОтбора.Пометка Тогда
				Организации.Добавить(ЭлементОтбора.Значение);
			КонецЕсли;
		КонецЦикла;
		
		Отбор.Вставить("Организация", Организации);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборБанковскийСчетКассаПредставление) Тогда
		БанковскиеСчетаКассы = Новый Массив();
		
		Для Каждого ЭлементОтбора ИЗ Объект.ОтборБанковскиеСчетаКассы Цикл
			Если ЭлементОтбора.Пометка Тогда
				БанковскиеСчетаКассы.Добавить(ЭлементОтбора.Значение);
			КонецЕсли;
		КонецЦикла;
		
		Отбор.Вставить("БанковскийСчетКасса", БанковскиеСчетаКассы);
	КонецЕсли;
	
	Отбор.Вставить("ВалютаИтогов", Объект.ВалютаИтогов);
	
	Если ОтборОбластиПланирования.Количество() Тогда
		Отбор.Вставить("ОбластьПланирования", ОтборОбластиПланирования);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ОтборВалюта) Тогда
		Отбор.Вставить("Валюта", Объект.ОтборВалюта);
	КонецЕсли;
	
	ПараметрыФормы.Отбор = Отбор;
	
	ИмяОтчета = "__РеестрПлатежейПоЗаявителю";
	
	ВнешнийОтчетСсылка = ПолучитьСсылкуНаВнешнийОтчетОбработкуПоИмениНаСервере(ИмяОтчета);
	ИмяОтчетаСлужебное = ДополнительныеОтчетыИОбработкиВызовСервера.ПодключитьВнешнююОбработку(ВнешнийОтчетСсылка);
	
	Если ЗначениеЗаполнено(ВнешнийОтчетСсылка) Тогда
		ОткрытьФорму("ВнешнийОтчет." + ИмяОтчетаСлужебное + ".Форма", ПараметрыФормы, ЭтаФорма);
	Иначе
		Сообщить("Внешний отчет с именем объекта " + ИмяОтчета + " не найден");	
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьСсылкуНаВнешнийОтчетОбработкуПоИмениНаСервере(ИмяОтчета)
	
	Возврат Справочники.ДополнительныеОтчетыИОбработки.НайтиПоРеквизиту("ИмяОбъекта", ИмяОтчета).Ссылка;

КонецФункции
//abaturina@syspod.ru #77028
